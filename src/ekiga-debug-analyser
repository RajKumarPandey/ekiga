#!/usr/bin/awk -f

# Usage: ekiga-debug-analyser <ekigaDebugOutput
#   where the output comes from ekiga -d 4
# Goal: Given a debug output file, show only the application packets
#   exchanged on the network.
#   This especially allows to conveniently compare two debug outputs
#   in terms of packets exchanged.
# Written by Eugen Dedu, 03 06 2009

/Sending PDU|PDU received/{
    inside = 1;
    # remove $1 (the date) and $2 (the time), and let $3 (the relative time)
    $1 = $2 = "";
    print " ========================", $0;
    next;
}

/^ *$/{  # empty line
    if (inside == 1) {
        print;
        inside = 2;  # wait for two empty lines, so that it prints the body too
    } else if (inside == 2) {
        print;
        inside = 0;
    }
    next;
}

{
    if (inside)
        print;
}
